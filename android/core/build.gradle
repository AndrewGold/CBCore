apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.0"


    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

configurations {
    ktlint
}

dependencies {

    ktlint "com.pinterest:ktlint:${otherVersions.ktlintVersion}"

    implementation(
            // kotlin
            "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlinVersion}",

            // ReactiveX 2
            "io.reactivex.rxjava2:rxjava:${versions.rxJavaVersion}",
            "io.reactivex.rxjava2:rxandroid:${versions.rxAndroidVersion}",
            "io.reactivex.rxjava2:rxkotlin:${versions.rxKotlinVersion}",

            // optional
            "com.gojuno.koptional:koptional:${versions.koptionalVersion}",

            // Logging
            "com.jakewharton.timber:timber:${versions.timberVersion}"
    )

    testImplementation(
            "junit:junit:${testVersions.junit4}"
    )

    androidTestImplementation(
            "androidx.test.ext:junit:${testVersions.androidXTestExtJunit}",
            "androidx.test:runner:${testVersions.androidXTestRunner}",
            "androidx.test:core:${testVersions.androidXTestCore}"
    )
}


task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

repositories {
    mavenCentral()
}
